[[new_properties]]
== New Properties

This specification makes use of the NAME property which is defined in <<RFC7986>>.

[[loctype]]
=== Location Type

Property name:: LOCATION-TYPE

Purpose:: To specify the type(s) of a location.

Value type:: The value type for this property is TEXT. The allowable values are
defined below.

Description:: This property MAY be specified in VLOCATION components and
provides a way to differentiate multiple locations. For example, it allows event
producers to provide location information for the venue and the parking.

Format Definition:: This property is defined by the following notation:
+
[source%unnumbered]
----
loctype      = "LOCATION-TYPE" loctypeparam ":"
               text *("," text)
               CRLF

loctypeparam   = *(";" other-param)
----
+
Multiple values may be used if the location has multiple purposes, for example a
hotel and a restaurant.
+
Values for this parameter are taken from the values defined in
<<RFC4589,section=3>>. New location types SHOULD be registered in the manner
laid down in section 5 of that specification.

[[participant-type]]
=== Participant Type

Property name:: PARTICIPANT-TYPE

Purpose:: To specify the type of participant.

Value type:: The value type for this property is TEXT. The allowable values are
defined below.

Property Parameters:: Non-standard parameters can be specified on this property.

Conformance:: This property MUST be specified once within a PARTICIPANT
component.

Description:: This property defines the type of participation in events or
tasks. Participants can be individuals or organizations, for example a soccer
team, the spectators, or the musicians.

Format Definition:: This property is defined by the following notation:
+
[source%unnumbered]
----
participanttype   = "PARTICIPANT-TYPE" partvalueparam ":"
                    partvalue CRLF

partvalue    = ("ACTIVE"
              / "INACTIVE"
              / "SPONSOR"
              / "CONTACT"
              / "BOOKING-CONTACT"
              / "EMERGENCY-CONTACT"
              / "PUBLICITY-CONTACT"
              / "PLANNER-CONTACT"
              / "PERFORMER"
              / "SPEAKER"
              / iana-token)     ; Other IANA-registered
                                ; values

partvalueparam   = *(";" other-param)
----

Example::
+
[source%unnumbered]
----
PARTICIPANT-TYPE:SPEAKER
----

The registered values for the PARTICIPANT-TYPE property have the meanings
described here:

ACTIVE:: A participant taking an active role - for example a team member.

INACTIVE:: A participant taking an inactive role - for example an audience
member.

SPONSOR:: A sponsor of the event. The ORDER parameter may be used with this
participant type to define the relative order of multiple sponsors.

CONTACT:: Contact information for the event. The ORDER parameter may be used
with this participant type to define the relative order of multiple contacts.

BOOKING-CONTACT:: Contact information for reservations or payment

EMERGENCY-CONTACT:: Contact in case of emergency

PUBLICITY-CONTACT:: Contact for publicity

PLANNER-CONTACT:: Contact for the event planner or organizer

PERFORMER:: A performer - for example the soloist or the accompanist. The ORDER
parameter may be used with this participant type to define the relative order of
multiple performers. For example, ORDER=1 could define the principal performer
or soloist.

SPEAKER:: Speaker at an event

[[restype]]
=== Resource Type

Property name:: RESOURCE-TYPE

Purpose:: To specify the type of resource.

Value type:: The value type for this property is TEXT. The allowable values are
defined below.

Format Definition:: This property is defined by the following notation:
+
[source%unnumbered]
----
restypeprop   = "RESOURCE-TYPE" restypeparam ":"
                restypevalue CRLF

restypevalue  = ("ROOM"
               / "PROJECTOR"
               / "REMOTE-CONFERENCE-AUDIO"
               / "REMOTE-CONFERENCE-VIDEO"
               / iana-token)     ; Other IANA-registered
               ; values

restypeparam   = *(";" other-param)
----

Description:: This property MAY be specified in VRESOURCE components and
provides a way to differentiate multiple resources.

The registered values are described below. New resource types SHOULD be
registered in the manner laid down in this specification.

ROOM:: A room for the event/meeting.

PROJECTOR:: Projection equipment.

REMOTE-CONFERENCE-AUDIO:: Audio remote conferencing facilities.

REMOTE-CONFERENCE-VIDEO:: Video remote conferencing facilities.

[[calendar-address]]
=== Calendar Address

Property name:: CALENDAR-ADDRESS

Purpose:: To specify the calendar address for a participant.

Value type:: CAL-ADDRESS

Property Parameters:: IANA-registered, or non-standard property parameters can
be specified on this property.

Conformance:: This property MAY be specified once within a PARTICIPANT
component.

Description:: This property provides a calendar user address for the
participant. If there is an ATTENDEE property with the same value then the
participant is schedulable.

Format Definition:: This property is defined by the following notation:
+
[source%unnumbered]
----
calendaraddress   = "CALENDAR-ADDRESS" caladdressparam ":"
               cal-address CRLF

caladdressparam   = *(";" other-param)
----

[[styled-description]]
=== Styled-Description

Property name:: STYLED-DESCRIPTION

Purpose:: This property provides for one or more rich-text descriptions to
replace that provided by the DESCRIPTION property.

Value type:: There is no default value type for this property. The value type
can be set to URI or TEXT. Other text-based value types can be used when defined
in the future. Clients MUST ignore any properties with value types they do not
understand.

Property Parameters:: IANA-registered, non-standard, id, alternate text
representation, format type, derived and language property parameters can be
specified on this property.

Conformance:: The property can be specified multiple times in the "VEVENT",
"VTODO", "VJOURNAL", "VFREEBUSY", "PARTICIPANT", or "VALARM" calendar
components.
+
If it does appear more than once there MUST be exactly one instance of the
property with no DERIVED parameter or DERIVED=FALSE. All others MUST have
DERIVED=TRUE.
+
Additionally, if there is one or more STYLED-DESCRIPTION property then the
DESCRIPTION property should be either absent or have the parameter DERIVED=TRUE.

Description:: This property supports rich-text descriptions, for example HTML.
Event publishers typically wish to provide more and better formatted information
about the event.
+
This property is used in the "VEVENT" and "VTODO" to capture lengthy textual
descriptions associated with the activity. This property is used in the
"VJOURNAL" calendar component to capture one or more textual journal entries.
This property is used in the "VALARM" calendar component to capture the display
text for a DISPLAY category of alarm, and to capture the body text for an EMAIL
category of alarm. In the PARTICIPANT component it provides a detailed
description of the participant.
+
VALUE=TEXT is used to provide rich-text inline as the property value.
+
VALUE=URI is used to provide a link to rich-text content which is expected to be
displayed inline as part of the event.
+
In either case the DESCRIPTION property should be absent or contain a plain text
rendering of the styled text.
+
Applications MAY attempt to guess the media type of the resource via inspection
of its content if and only if the media type of the resource is not given by the
"FMTTYPE" parameter. If the media type remains unknown, calendar applications
SHOULD treat it as type "text/html" and process the content as defined in
<<W3C.REC-html51-20171003>>.
+
Multiple STYLED-DESCRIPTION properties may be used to provide different formats
or different language variants. However all but one MUST have DERIVED=TRUE.

Format Definition:: This property is defined by the following notation:
+
[source%unnumbered]
----
styleddescription = "STYLED-DESCRIPTION" styleddescparam ":"
                  styleddescval CRLF

styleddescparam   = *(
                  ; The following is REQUIRED,
                  ; but MUST NOT occur more than once.
                  ;
                  (";" "VALUE" "=" ("URI" / "TEXT")) /
                  ;
                  ; The following are OPTIONAL,
                  ; but MUST NOT occur more than once.
                  ;
                  (";" altrepparam) / (";" languageparam) /
                  (";" fmttypeparam) / (";" derivedparam) /
                  ;
                  ; the following is OPTIONAL
                  ; and MAY occur more than once
                  ;
                  (";" other-param)
                  )

styleddescval   = ( uri / text )
;Value MUST match value type
----

Example:: The following is an example of this property. It points to an html
description.
+
[source%unnumbered]
----
STYLED-DESCRIPTION;VALUE=URI:http://example.org/desc001.html
----

[[structured-data]]
=== Structured-Data

Property Name:: STRUCTURED-DATA

Purpose:: This property specifies ancillary data associated with the calendar
component.

Value Type:: There is no default value type for this property. The value type
can be set to TEXT, BINARY or URI

Property Parameters:: IANA-registered, non-standard, inline encoding and value
data type property parameters can be specified on this property. The format type
and schema parameters can be specified on this property and MUST be present for
text or inline binary encoded content information.

Conformance:: This property can be specified multiple times in an iCalendar
object. Typically it would be used in "VEVENT", "VTODO" or "VJOURNAL" calendar
components.

Description:: The existing properties in iCalendar cover key elements of events
and tasks such as start time, end time, location, summary, etc. However,
different types of events often have other specific "fields" that it is useful
to include in the calendar data. For example, an event representing an airline
flight could include the airline, flight number, departure and arrival airport
codes, check-in and gate-closing times etc. As another example, a sporting event
might contain information about the type of sport, the home and away teams, the
league the teams are in, information about nearby parking, etc.
+
This property is used to specify ancillary data in some structured format either
directly (inline) as a "TEXT" or "BINARY" value or as a link via a "URI" value.
+
Rather than define new iCalendar properties for the variety of event types that
might occur, it would be better to leverage existing schemas for such data. For
example, schemas available at https://schema.org include different event types.
By using standard schemas, interoperability can be improved between calendar
clients and non-calendaring systems that wish to generate or process the data.
+
This property allows the direct inclusion of ancillary data whose schema is
defined elsewhere. This property also includes parameters to clearly identify
the type of the schema being used so that clients can quickly and easily spot
what is relevant within the calendar data and present that to users or process
it within the calendaring system.
+
iCalendar does support an "ATTACH" property which can be used to include
documents or links to documents within the calendar data. However, that property
does not allow data to be included as a "TEXT" value (a feature that
"STRUCTURED-DATA" does allow), plus attachments are often treated as "opaque"
data to be processed by some other system rather than the calendar client. Thus
the existing "ATTACH" property is not sufficient to cover the specific needs of
inclusion of schema data. Extending the "ATTACH" property to support a new value
type would likely cause interoperability problems. Additionally some
implementations manage attachments by stripping them out and replacing with a
link to the resource. Thus a new property to support inclusion of schema data is
warranted.

Format Definition:: This property is defined by the following notation:
+
[source%unnumbered]
----
sdataprop    = "STRUCTURED-DATA" sdataparam
                 (
                   ";" "VALUE" "=" "TEXT"
                   ":" text
                 ) /
                 (
                   ";" "ENCODING" "=" "BASE64"
                   ";" "VALUE" "=" "BINARY"
                   ":" binary
                 ) /
                 (
                   ";" "VALUE" "=" "URI"
                   ":" uri
                 )
                 CRLF

sdataparam  = *(
                ;
                ; The following is OPTIONAL for a URI value,
                ; REQUIRED for a TEXT or BINARY value,
                ; and MUST NOT occur more than once.
                ;
                (";" fmttypeparam) /
                (";" schemaparam) /
                ;
                ; The following is OPTIONAL,
                ; and MAY occur more than once.
                ;
                (";" other-param)
                ;
             )
----

Example:: The following is an example of this property:
+
[source%unnumbered]
----
STRUCTURED-DATA;FMTTYPE=application/ld+json;
 SCHEMA="https://schema.org/SportsEvent";
 VALUE=TEXT:{\n
  "@context": "http://schema.org"\,\n
  "@type": "SportsEvent"\,\n
  "homeTeam": "Pittsburgh Pirates"\,\n
  "awayTeam": "San Francisco Giants"\n
}\n
----
